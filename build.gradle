import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java-library'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.7'
    id 'com.modrinth.minotaur' version '2.+' apply false
    id 'net.darkhax.curseforgegradle' version '1.+' apply false
}

interface PlatformInfoExtension {
    Property<String> getPlatform();
    ListProperty<String> getVersionsForCurseForgeAndModrinth();
    Property<String> getSupportedVersions();
}

subprojects { Project p ->
    apply plugin: 'java'
    if (!p.name.endsWith('2-neoforge')) {
        apply plugin: 'idea'
    }
    apply plugin: 'com.modrinth.minotaur' // https://stackoverflow.com/a/50153617
    apply plugin: 'net.darkhax.curseforgegradle'

    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven {
            url "https://api.modrinth.com/maven"
            content {
                includeGroup "maven.modrinth"
            }
        }
    }

    p.extensions.create('platformInfo', PlatformInfoExtension)

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    p.archivesBaseName = "FastEntityTransfer"
    p.version = project.version

    p.tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
    }

    p.processResources {
        // Exclude .cache directory which is generated by DataGen.
        exclude '.cache'
    }

    p.afterEvaluate { Project pAfter ->

        if (!pAfter.name.endsWith('common')) {
            String baseProject = ':' + pAfter.name.substring(0, pAfter.name.indexOf('-')) + '-common'

            if (pAfter.name.substring(0, pAfter.name.indexOf('-')) == "1.20.1") {
                baseProject = ":1.20.x-common"
            }

            pAfter.compileJava {
                source(pAfter.project(baseProject).sourceSets.main.allSource)
            }
            pAfter.processResources {
                from project(baseProject).sourceSets.main.resources
            }
        }

        def projectExt = pAfter.extensions.platformInfo as PlatformInfoExtension

        pAfter.archivesBaseName = "${mod_id}-${projectExt.platform.get().toLowerCase()}-${projectExt.supportedVersions.get()}"

        pAfter.modrinth {
            try {
                token = file('../API Tokens/modrinth.md').text
            } catch (Exception ignored) {
                token = ""
                System.err("Could not get Modrinth token")
            }
            projectId = "${modrinth_id}"
            loaders = [ projectExt.platform.get().toLowerCase(Locale.ROOT) ]
            versionNumber = project.version
            versionName = getArchivesBaseName() + "-" + getVersion()
            versionType = "${release_type}"
            if (!pAfter.name.endsWith('common')) {
                changelog = file("${project.getName()}/changelog.md").text
            }
            uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
            gameVersions = projectExt.versionsForCurseForgeAndModrinth.getOrElse([])
            dependencies {
                if (pAfter.name.endsWith('fabric')) {
                    required.project "fabric-api"
                } else if (pAfter.name.endsWith('quilt')) {
                    required.project "qsl"
                }
            }
        }
        if ("Vanilla" == projectExt.platform.get()) {
            // Disable modrinth task for common sub-projects
            pAfter.tasks.named("modrinth") {
                enabled = false
            }
        } else {
            pAfter.tasks.register('publishCurseForge', TaskPublishCurseForge) {
                group = 'publishing'
                disableVersionDetection()
                try {
                    apiToken = file('../API Tokens/fastentitytransfer.md')
                } catch (Exception ignored) {
                    apiToken = ""
                    System.err("Could not get CurseForge token")
                }
                def projectId = "${cf_id}".toString()
                def mainFile = upload(projectId, file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"))
                mainFile.changelogType = 'markdown'
                if (!pAfter.name.endsWith('common')) {
                    mainFile.changelog = file("${project.getName()}/changelog.md").text
                }
                mainFile.addGameVersion('Client', 'Server')
                mainFile.addModLoader(projectExt.platform.get())
                projectExt.versionsForCurseForgeAndModrinth.getOrElse([]).each {
                    mainFile.addGameVersion(it)
                }
                mainFile.releaseType = "${release_type}"
                if (pAfter.name.endsWith('fabric')) {
                    mainFile.addRequirement("fabric-api")
                } else if (pAfter.name.endsWith('quilt')) {
                    mainFile.addRequirement("qsl")
                }
            }
        }
    }

}
